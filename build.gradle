plugins {
    id 'java-library'
    id 'idea'
    id 'maven-publish'
    id 'net.neoforged.gradle.userdev' version '7.0.145'
}

version = mod_version
group = mod_group_id

base {
    archivesName = mod_id
}

// Mojang ships Java 21 to end users starting in 1.20.5, so mods should target Java 21.
java.toolchain.languageVersion = JavaLanguageVersion.of(21)

println "Java: ${System.getProperty 'java.version'}, JVM: ${System.getProperty 'java.vm.version'} (${System.getProperty 'java.vendor'}), Arch: ${System.getProperty 'os.arch'}"

minecraft {
//    accessTransformers {
//        file('src/main/resources/META-INF/accesstransformer.cfg')
//    }
}

runs {
    configureEach {
        systemProperty 'forge.logging.markers', 'REGISTRIES'
        systemProperty 'forge.logging.console.level', 'debug'
        modSource project.sourceSets.main
    }

    client {
        systemProperty 'forge.enabledGameTestNamespaces', project.mod_id
    }

    server {
        systemProperty 'forge.enabledGameTestNamespaces', project.mod_id
        programArgument '--nogui'
    }

    gameTestServer {
        systemProperty 'forge.enabledGameTestNamespaces', project.mod_id
    }

    data {
        programArguments.addAll '--mod', project.mod_id, '--all', '--output', file('src/generated/resources/').getAbsolutePath(), '--existing', file('src/main/resources/').getAbsolutePath()
    }
}

// Include resources generated by data generators.
sourceSets.main.resources { srcDir 'src/generated/resources' }

configurations {
    runtimeClasspath.extendsFrom localRuntime
}

repositories {
    flatDir {
        dir 'libs'
    }

    maven { url = "https://maven.enginehub.org/repo/" }
    maven {
        url = "https://dl.cloudsmith.io/public/geckolib3/geckolib/maven/"
        content {
            includeGroup("software.bernie.geckolib")
        }
    }
    maven { url = "https://maven.theillusivec4.top" }
    maven { url = "https://cursemaven.com" }
    maven { url = "https://maven.blamejared.com" }
    maven { url = "https://maven.kosmx.dev/" }
    maven { url = "https://code.redspace.io/releases" }
}

dependencies {
    // FORGE ******************************************************************************************************
    implementation "net.neoforged:neoforge:${neo_version}"

    // IRONS SPELLS N SPELLBOOKS **********************************************************************************
    implementation "io.redspace:irons_spellbooks:${irons_spells_version}"

    // GECKOLIB ***************************************************************************************************
    implementation "software.bernie.geckolib:geckolib-neoforge-${minecraft_version}:${geckolib_version}"

    // CAELUS *****************************************************************************************************
    localRuntime "com.illusivesoulworks.caelus:caelus-neoforge:${caelus_version}"

    // PLAYER ANIMATOR ********************************************************************************************
    localRuntime "dev.kosmx.player-anim:player-animation-lib-forge:${player_animator_version}"

    // ALLTHEMODIUM  ***************************************************************************************************
    implementation "curse.maven:allthemodium-${allthemodium_version}"
    runtimeOnly "curse.maven:ato-${alltheores_version}"

    // JEI ********************************************************************************************************
    compileOnly "mezz.jei:jei-${jei_mc_version}-common-api:${jei_version}"
    compileOnly "mezz.jei:jei-${jei_mc_version}-neoforge-api:${jei_version}"
    runtimeOnly "mezz.jei:jei-${jei_mc_version}-neoforge:${jei_version}"
    //localRuntime "curse.maven:just-enough-effect-descriptions-jeed-532286:4599099")
    // CURIOS *****************************************************************************************************
    //todo: 1.21: find correct maven for new curios
    implementation "curse.maven:adorned-1036809:5546365"

    //QOL
    localRuntime "curse.maven:mouse-tweaks-60089:5437296"

}

tasks.withType(ProcessResources).configureEach {
    var replaceProperties = [
            minecraft_version      : minecraft_version,
            minecraft_version_range: minecraft_version_range,
            neo_version            : neo_version,
            neo_version_range      : neo_version_range,
            loader_version_range   : loader_version_range,
            caelus_version         : caelus_version,
            geckolib_version       : geckolib_version,
            player_animator_version: player_animator_version,
            irons_spells_version   : irons_spells_version,
            mod_id                 : mod_id,
            mod_name               : mod_name,
            mod_version            : mod_version,
            mod_license            : mod_license,
            mod_authors            : mod_authors,
            //mod_credits            : mod_credits,
            mod_description        : mod_description/*,
            mod_display_url        : mod_display_url,
            mod_issue_tracker      : mod_issue_tracker,
            mod_logo_file          : mod_logo_file*/
    ]
    inputs.properties replaceProperties

    filesMatching(['META-INF/neoforge.mods.toml']) {
        expand replaceProperties
    }
}

jar {
    manifest {
        attributes([
                "Specification-Title"     : mod_name,
                "Specification-Vendor"    : mod_authors,
                "Specification-Version"   : "1", // We are version 1 of ourselves
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : project.jar.archiveVersion,
                "Implementation-Vendor"   : mod_authors,
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
        ])
    }
    exclude('**/*.psd')
    exclude('**/*.bbmodel')
}

java {
    withSourcesJar()
}

tasks.register('apiJar', Jar) {
    archiveClassifier.set('api')
    include 'io/redspace/ironsspellbooks/api/**/*'
    from sourceSets.main.allSource
    from sourceSets.main.output
}

artifacts {
    archives apiJar
    archives sourcesJar
}

publishing {
    tasks.publish.dependsOn 'build'
    publications {

        mavenJava(MavenPublication) {
            artifactId project.archivesBaseName
            artifact jar
            artifact sourcesJar
            artifact apiJar
        }
    }

    repositories {
        maven {
            name = "redspace"

            if (project.version.endsWith('-SNAPSHOT')) {
                url uri("https://code.redspace.io/snapshots")
            } else {
                url uri("https://code.redspace.io/releases")
            }

            credentials {
                username = System.getenv("MAVEN_USER")
                password = System.getenv("MAVEN_PASS")
            }
            authentication {
                basic(BasicAuthentication)
            }
        }
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8' // Use the UTF-8 charset for Java compilation
}

idea {
    module {
        downloadSources = true
        downloadJavadoc = true
    }
}